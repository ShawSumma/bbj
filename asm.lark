
start: /\n/* instrs

instrs: (instr | label | raw | align | local | include | loop | macro)*

instr: NAME args "\n"+

label: NAME ":" "\n"*
raw: ".raw" raw_args "\n"+
align: ".align" arg "\n"+
local: ".local" NAME ","? arg "\n"+
include: ".include" STR "\n"+
loop: ".loop" NAME ","? args1 "\n"+ instrs ".end" "\n"+
macro: ".macro" NAME (NAME (","? NAME)*)? "\n"+ instrs ".end" "\n"+

raw_args: ((arg | STR) (","? (arg | STR))*)?
args: (arg (","? arg)*)?
args1: arg (","? arg)*

arg: math_add

math_add: math_mul (ADD_OP math_mul)*
math_mul: single (MUL_OP single)*
single: NAME | BIN | HEX | INT

ADD_OP: "+" | "-"
MUL_OP: "*" | "/" | "%"

BIN: /\b0b[01]+\b/
HEX: /\b0x[0-9A-F]+\b/
INT: /\b0|[1-9][0-9]*\b/

NAME: /\b[a-zA-Z_][a-zA-Z_0-9]*\b/

%import common.ESCAPED_STRING -> STR

%ignore /#.*\n/
%ignore "\t"
%ignore " "
